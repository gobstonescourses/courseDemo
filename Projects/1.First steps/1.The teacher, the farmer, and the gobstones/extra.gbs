procedure GoToNextCell() 
 {
   AuxSacarCursor()
   AuxPasarASiguienteCelda() 
   AuxPonerCursor()
 }

procedure DropAGreenStone() {
   if (nroBolitas(Negro) == 2) 
          { Poner(Verde) } 
     else { BOOM("This is NOT a place to drop a green stone") } 
}

procedure GrabRedStones() {
    if (hayBolitas(Rojo)) 
          { Sacar(Rojo) } 
     else { BOOM("There is no red stone to grab") } }

procedure GoToOriginChangingStones() {
    while (auxHaySiguienteCelda()) {
        AuxPasarASiguienteCelda()
        AuxCambiarVerdePorAzulYRoja() 
    }
    IrAlBorde(Oeste); IrAlBorde(Sur)
}

procedure GoToOrigin() 
  { 
    AuxSacarCursor()   
    IrAlBorde(Oeste); IrAlBorde(Sur) 
    AuxPonerCursor()
  }

procedure AuxSacarCursor() { 
  if (nroBolitas(Negro) < 2) { BOOM("There are not enough black stones") }
  repeat(2) { Sacar(Negro) } 
}

procedure AuxPonerCursor() { 
  if (nroBolitas(Negro) >= 2) { BOOM("There already are enough black stones") }
  repeat(2) { Poner(Negro) } 
}

procedure AuxCambiarVerdePorAzulYRoja() { 
    if (hayBolitas(Verde)) 
            { Sacar(Verde); Poner(Azul); Poner(Rojo) } 
       else { BOOM("Green stones cannot be changed if there are not all of them present") } 
}       

function auxHaySiguienteCelda() 
    { return (puedeMover(Este) || puedeMover(Norte)) }

procedure AuxPasarASiguienteCelda() {
    if (puedeMover(Este)) 
         { Mover(Este) } 
    else { if (puedeMover(Norte))
                { Mover(Norte) IrAlBorde(Oeste) } 
           else { BOOM("There are no more cells: cannot go to the next one") }
         }
}
